plugins {
	id 'com.diffplug.spotless' version '6.11.0'
}

/**
 * Create a new FileCollection that is similar to the input, but replaces any
 * non-directories (assumed to be zip files) with it's contents
 */
def expandZips(FileCollection inputs) {
	FileCollection retval = files()
	inputs.forEach{f ->
		if (f.isDirectory()) {
			retval = retval + files(f)
		} else {
			retval = retval + zipTree(f)
		}
	}
	return retval;
}

/**
 * Split a string by whitespace, taking quoting and escaping into account
 * @see https://stackoverflow.com/questions/20410854/how-to-pass-parameters-or-arguments-into-a-gradle-task/48370451#48370451
 */
def List<String> splitIntoTokens(String commandLine) {
	String regex = "(([\"']).*?\\2|(?:[^\\\\ ]+\\\\\\s+)+[^\\\\ ]+|\\S+)";
	java.util.regex.Matcher matcher = java.util.regex.Pattern.compile(regex).matcher(commandLine);
	ArrayList<String> result = new ArrayList<>();
	while (matcher.find()) {
		result.add(matcher.group());
	}
	return result
}

def String fxPlatform() {
	def currentOS = org.gradle.internal.os.OperatingSystem.current()
	if (currentOS.isWindows()) {
		return 'win'
	} else if (currentOS.isLinux()) {
		return 'linux'
	} else if (currentOS.isMacOsX()) {
		return 'mac'
	}
}

def String fxVersion() {
	return '17'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'com.diffplug.spotless'
	
	sourceCompatibility = (org.gradle.api.JavaVersion.current() == org.gradle.api.JavaVersion.VERSION_1_8 ? 8 : 9)
	[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
	[compileJava, compileTestJava]*.options*.deprecation = true
	[compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:unchecked"]
	
	group = "name.rayrobdod"
	version = 'SNAPSHOT'
	javadoc.options.overview = new File(sourceSets.main.java.srcDirs[0], 'overview.html')
	javadoc.options.addBooleanOption('Xdoclint:-syntax', true)
	if (JavaVersion.current().isJava8()) {
		javadoc.options.links("https://docs.oracle.com/javase/8/docs/api/")
		javadoc.options.links("https://docs.oracle.com/javase/8/javafx/api/")
	} else if (JavaVersion.current().isJava11()) {
		javadoc.options.links("https://docs.oracle.com/en/java/javase/11/docs/api/")
		javadoc.options.links("https://openjfx.io/javadoc/11/")
	} else {
		// https://download.java.net/java/early_access/jdk12/docs/api/
	}
	
	repositories {
		mavenCentral()
	}
	
	dependencies {
		testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
		testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
		testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"
	}
	
	if (! project.hasProperty("noExternalJavafx")) {
		dependencies {
			compileOnly "org.openjfx:javafx-base:${fxVersion()}:${fxPlatform()}"
			compileOnly "org.openjfx:javafx-graphics:${fxVersion()}:${fxPlatform()}"
			compileOnly "org.openjfx:javafx-controls:${fxVersion()}:${fxPlatform()}"
			compileOnly "org.openjfx:javafx-swing:${fxVersion()}:${fxPlatform()}"
			compileOnly "org.openjfx:javafx-media:${fxVersion()}:${fxPlatform()}"
			
			testImplementation "org.openjfx:javafx-base:${fxVersion()}:${fxPlatform()}"
			testImplementation "org.openjfx:javafx-graphics:${fxVersion()}:${fxPlatform()}"
			testImplementation "org.openjfx:javafx-controls:${fxVersion()}:${fxPlatform()}"
			testImplementation "org.openjfx:javafx-swing:${fxVersion()}:${fxPlatform()}"
			testImplementation "org.openjfx:javafx-media:${fxVersion()}:${fxPlatform()}"
		}
	}

	if (JavaVersion.current().isJava9Compatible()) {
		sourceSets {
			main {
				java {
					srcDirs 'src/main/java', 'src/main/java-9+'
				}
			}
		}
		
		compileJava {
			doFirst {
				options.compilerArgs = [
				'--module-path', classpath.asPath,
				]
				classpath = files()
			}
		}
		javadoc {
			doFirst {
				options.modulePath(new java.util.ArrayList(classpath.files))
				classpath = files()
			}
		}
	}
	
	spotless {
		java {
			importOrder 'java', 'javax', 'javafx', 'org.junit', 'org.testfx', '', 'name.rayrobdod'
			removeUnusedImports()
			licenseHeaderFile '../spotless-license-java', 'package|module'
			endWithNewline()
			replaceRegex 'one blank line after package line', '(package .+;)\n+import', '$1\n\nimport'
			replaceRegex 'one blank line after import lists', '(import .+;\n\n)\n+', '$1'
		}
	}
	
	jar.manifest.attributes (
		  'Implementation-Title': name
		, 'Implementation-Version': version
		, 'Implementation-Vendor': group
	)
	
	// Run the thing that will tell me why the java will not compile
	// before running the thing that doesn't tell my why the java
	//     will not compile and prevents the first thing from running
	tasks.withType(com.diffplug.gradle.spotless.SpotlessTask) {mtask ->
		mtask.mustRunAfter(compileJava)
		mtask.mustRunAfter(compileTestJava)
	}
	
	test {
		enableAssertions = true
		useJUnitPlatform {
			// the `robot` tag refers to tests that make use of `org.testfx.api.FxRobot`
			// This hijacks a user's cursor for the duration of the tests, which may be undesirable
			if (project.hasProperty("excludeRobotTests")) {
				excludeTags 'robot'
			}
		}
	}
}

project(":core") {
	project.ext.mainClassName = null
	project.ext.artifactId = "fxfightstage"
	
	jar.manifest.attributes (
		  'Automatic-Module-Name': 'name.rayrobdod.fightStage.core'
		, 'Implementation-Title': project.ext.artifactId
	)
}

project(":demo") {
	project.ext.mainClassName = null
	project.ext.artifactId = "fxfightstage-demo"
	
	//TODO: convert to linksOffline after deciding where these will be online
	javadoc.options.links(project(":core").javadoc.destinationDir.toURI().toString())
	
	dependencies {
		implementation project(":core")
	}
	
	jar.manifest.attributes (
		'Created-By' : "Gradle ${gradle.gradleVersion}",
		  'Automatic-Module-Name': 'name.rayrobdod.fightStage.demo'
		, 'Implementation-Title': project.ext.artifactId
	)
}

project(":samples") {
	project.ext.mainClassName = "name.rayrobdod.fightStage.previewer.Main"
	project.ext.artifactId = "fxfightstage-samples"
	
	//TODO: convert to linksOffline after deciding where these will be online
	javadoc.options.links(project(":core").javadoc.destinationDir.toURI().toString())
	javadoc.options.links(project(":demo").javadoc.destinationDir.toURI().toString())
	
	dependencies {
		implementation project(":core")
		implementation project(":demo")
		testImplementation project(":core").sourceSets.test.output
	}
	
	if (! project.hasProperty("noExternalJavafx")) {
		dependencies {
			implementation "org.openjfx:javafx-base:${fxVersion()}:${fxPlatform()}"
			implementation "org.openjfx:javafx-graphics:${fxVersion()}:${fxPlatform()}"
			implementation "org.openjfx:javafx-controls:${fxVersion()}:${fxPlatform()}"
			implementation "org.openjfx:javafx-swing:${fxVersion()}:${fxPlatform()}"
			implementation "org.openjfx:javafx-media:${fxVersion()}:${fxPlatform()}"
		}
	}
	
	jar.manifest.attributes (
		  'Main-Class': project.mainClassName
		, 'Automatic-Module-Name': 'name.rayrobdod.fightStage.samples'
		, 'Implementation-Title': project.ext.artifactId
	)
	
	task run(dependsOn: classes, type: JavaExec) {
		mainClass = project.mainClassName
		classpath = sourceSets.main.runtimeClasspath
		args = splitIntoTokens(project.hasProperty("args") ? project.args : "")
		standardInput = System.in
		ignoreExitValue = true
	}
	
	//example: `gradlew runRasterize "-Pargs=--spell=Tornado --d=out.png --fps=60 --bitDepth=8 --no-smooth"`
	task runRasterize(dependsOn: classes, type: JavaExec) {
		mainClass = "name.rayrobdod.fightStage.rasterizer.Main"
		classpath = sourceSets.main.runtimeClasspath
		args = splitIntoTokens(project.hasProperty("args") ? project.args : "")
		standardInput = System.in
		ignoreExitValue = true
	}
	if (JavaVersion.current().isJava9Compatible()) {
		run {
			doFirst {
				jvmArgs = [
					'--module-path', classpath.asPath,
					'--module', "name.rayrobdod.fightStage.demo/${project.mainClassName}"
				]
				classpath = files()
			}
		}
	}

	if (JavaVersion.current().isJava9Compatible()) {
		task extractLicenseToDir(type: Copy) {
			from "$rootDir/LICENSE.txt"
			into "$buildDir/tmp/mod/legal"
		}
		task extractSoundToDir(type: Copy) {
			from "$projectDir/src/main/resources/name/rayrobdod/fightStage/sounds/swing.wav"
			into "$buildDir/tmp/mod/libs"
		}
		task mod(type: Exec) {
			def outFile = project.layout.buildDirectory.dir(project.libsDirName).map{f -> f.file("fightstage-samples.jmod")}
			dependsOn 'jar'
			dependsOn 'extractLicenseToDir'
			dependsOn 'extractSoundToDir'
			dependsOn 'cleanMod'
			workingDir buildDir
			outputs.file outFile
			
			commandLine "${System.properties.'java.home'}/bin/jmod",
					'create',
					'--class-path', "${jar.archivePath}",
					'--legal-notices', "$buildDir/tmp/mod/legal",
					'--libs', "$buildDir/tmp/mod/libs",
					outFile.get().toString()
		}
		
		
		task link(type: Exec) {
			def outFile = project.layout.buildDirectory.dir(project.distsDirName).map{f -> f.dir("jlink")}
			dependsOn 'mod'
			dependsOn 'cleanLink'
			workingDir buildDir
			outputs.file outFile
			
			commandLine "${System.properties.'java.home'}/bin/jlink",
					'--module-path', "${project.configurations.runtimeClasspath.asPath}${File.pathSeparatorChar}libs/fightstage-samples.jmod${File.pathSeparatorChar}${System.properties.'java.home'}/jmods",
					'--add-modules', 'name.rayrobdod.fightStage.samples',
					'--launcher', "samples=name.rayrobdod.fightStage.demo/${project.mainClassName}",
					'--output', outFile.get().toString(),
					//'--bind-services',
					'--strip-debug',
					'--compress', '0',
					'--no-header-files',
					'--no-man-pages'
		}
		assemble.dependsOn(link)
	}
}
