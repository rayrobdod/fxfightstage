buildscript {
	dependencies {
		classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
	}
}
plugins {
	id "com.diffplug.gradle.spotless" version "3.6.0"
}
apply plugin: 'java'
apply plugin: 'org.junit.platform.gradle.plugin'

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.deprecation = true
[compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:unchecked"]

// name = 'fightStage'
version = 'SNAPSHOT'
project.ext.mainClassName = "name.rayrobdod.fightStage.previewer.Main"
javadoc.options.overview = new File(sourceSets.main.java.srcDirs[0], 'overview.html')

repositories {
	mavenCentral()
}

dependencies {
	testCompile("org.junit.jupiter:junit-jupiter-api:5.0.2")
	testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.2")
}

jar.manifest.attributes (
	  'Main-Class': project.mainClassName
	, 'Implementation-Title': name
	, 'Implementation-Version': version
	, 'Implementation-Vendor': 'com.rayrobdod'
)

spotless {
	java {
		importOrder 'java', 'javax', 'javafx', 'org.junit', '', 'name.rayrobdod'
		removeUnusedImports()
		endWithNewline()
	}
}

task run(dependsOn: classes, type: JavaExec) {
	main = project.mainClassName
	classpath = sourceSets.main.runtimeClasspath
	standardInput = System.in
	ignoreExitValue = true
}

// Run the thing that will tell me why the java will not compile
// before running the thing that doesn't tell my why the java
//     will not compile and prevents the first thing from running
tasks.withType(com.diffplug.gradle.spotless.SpotlessTask) {mtask ->
	mtask.mustRunAfter(compileJava)
	mtask.mustRunAfter(compileTestJava)
}
